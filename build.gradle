plugins {
    id 'java'
    id 'groovy'
    id 'checkstyle'
    id 'distribution'
    id 'org.omegat.gradle' version "1.2.5"
}

version = '2020.2.1-SNAPSHOT'

ext {
    javaVersion = '1.8'
}

omegat {
    version '5.2.0'
    pluginClass "tokyo.northside.omegat.textra.OmegatTextraMachineTranslation"
}

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

dependencies {
    compile 'oauth.signpost:signpost-core:1.2.1.2'
    compile 'oauth.signpost:signpost-commonshttp4:1.2.1.2'
    compile 'org.apache.httpcomponents:httpclient:4.5.12'
    compile 'org.json:json:20160810'
    compile fileTree(dir: 'lib', include: '*.jar')
    compile 'org.slf4j:slf4j-api:1.7.25'
    compileOnly 'commons-io:commons-io:2.5'
    compileOnly 'commons-lang:commons-lang:2.6'
    testImplementation 'commons-io:commons-io:2.5'
    testImplementation 'commons-lang:commons-lang:2.6'
    testImplementation "org.codehaus.groovy:groovy-all:3.0.1"
    testImplementation 'org.testng:testng:6.9.10'
}
test.useTestNG()

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

tasks.withType(Checkstyle) {
    exclude('**/dialog/TextraOptionDialog.java')
}

task changedOnBranch {
    description = 'List files that have been modified on this git branch.'
    doLast {
        ext.files = project.files(gitModifiedFiles())
        ext.files.each { println(it) }
    }
}

task spotlessChangedApply {
    description = 'Apply code formatting to files that have been changed on the current branch.'
    finalizedBy 'spotlessApply'
    dependsOn changedOnBranch
    doFirst {
        spotlessJava.target = changedOnBranch.files.findAll {
            it.path.endsWith('.java')
        }
    }
}

distTar {
  compression = Compression.GZIP
}

distributions {
    main {
        contents {
            from(jar)
            from('README.md', 'CHANGELOG.md', 'COPYING', 'DEVELOP.md')
        }
    }
}
